#Maintainer: Alex Filgueira <alexfilgueira@antergos.com>

pkgname=cnchi
_pkgname=Cnchi
pkgver=1
pkgrel=1
pkgdesc='Antergos Installer'
arch=('any')
license=('GPL3')
depends=('iso-codes' 'geoip2-database' 'gptfdisk' 'python' 'pyalpm'
         'python-cairo' 'python-chardet' 'python-dbus' 'python-defusedxml' 
         'python-feedparser' 'python-geoip2' 'python-gobject' 'python-idna'
         'python-libnacl' 'python-mako' 'python-maxminddb' 'python-pyparted'
         'python-requests' 'upower' 'webkit2gtk' 'python-urllib3')
conflicts=('cnchi')

package() {
    cd ..

    install -d ${pkgdir}/usr/share/{cnchi,locale}
    install -Dm755 "bin/cnchi" "${pkgdir}/usr/bin/cnchi"
    install -Dm755 "scripts/testing.sh" "${pkgdir}/usr/bin/cnchi-dev"
    install -Dm755 "cnchi.desktop" "${pkgdir}/usr/share/applications/cnchi.desktop"
    install -Dm644 data/images/antergos/antergos-icon.png "${pkgdir}/usr/share/pixmaps/cnchi.png"

    for i in src bin data scripts ui; do
        cp -R ${i} "${pkgdir}/usr/share/cnchi/"
    done

    for files in po/*.po; do
        if [ -f "${files}" ] && [ "${files}" != 'po/cnchi.pot' ]; then
            STRING_PO=`echo ${files#*/}`
            STRING=`echo ${STRING_PO%.po}`
            mkdir -p ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES
            msgfmt $files -o ${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/cnchi.mo
            echo "${STRING} installed..."
        fi
    done
}
